apply plugin: "java"

version = "0.1.0"

sourceSets.main.java.srcDirs = ["src"]

repositories{
    mavenCentral()
    maven{ url = "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
    maven{ url = "https://www.jitpack.io" }
}

java{
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

ext{
    //the build number that this mod is made for
    mindustryVersion = "v154.2"
    //windows sucks
    isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
    sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

//java 8 backwards compatibility flag
allprojects{
    tasks.withType(JavaCompile).tap{
        configureEach{
            options.compilerArgs.addAll(["--release", "8"])
        }
    }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
}

//force arc version
configurations.configureEach{
    resolutionStrategy.eachDependency{ details ->
        if(details.requested.group == "com.github.Anuken.Arc"){
            details.useVersion "$mindustryVersion"
        }
    }
}

tasks.register("jarAndroid"){
    dependsOn "jar"

    doLast{
        if(!sdkRoot || !new File(sdkRoot).exists()){
            logger.lifecycle("No valid Android SDK found (ANDROID_HOME/ANDROID_SDK_ROOT). Skipping Android jar generation.")
            return
        }

        def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find{ f -> new File(f, "android.jar").exists() }

        if(!platformRoot){
            logger.lifecycle("No android.jar found in Android SDK platforms/. Skipping Android jar generation.")
            return
        }

        //collect dependencies needed for desugaring
        def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect{ "--classpath $it.path" }.join(" ")

        def d8 = isWindows ? "d8.bat" : "d8"

        if(!System.getenv("PATH")?.split(File.pathSeparator)?.any{ new File(it, d8).exists() }){
            logger.lifecycle("No '$d8' found in PATH. Skipping Android jar generation.")
            return
        }

        //dex and desugar files - this requires d8 in your PATH
        "$d8 $dependencies --min-api 14 --output ${project.name}Android.jar ${project.name}Desktop.jar"
            .execute(null, new File("build/libs")).waitForProcessOutput(System.out, System.err)
    }
}

jar{
    archiveFileName = "${project.name}Desktop.jar"

    from{
        configurations.runtimeClasspath.collect{ it.isDirectory() ? it : zipTree(it) }
    }

    from(projectDir){
        include "mod.hjson"
    }

    from("assets/"){
        include "**"
    }
}

tasks.register("deploy", Jar){
    dependsOn jarAndroid
    dependsOn jar
    archiveFileName = "${project.name}.jar"

    from{ zipTree("build/libs/${project.name}Desktop.jar") }
    from{
        def androidJar = file("build/libs/${project.name}Android.jar")
        androidJar.exists() ? zipTree(androidJar) : []
    }

    doLast{
        delete{
            delete "build/libs/${project.name}Android.jar"
        }
    }
}
